plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'java.revision'
version = '0.0.1-SNAPSHOT'
description = 'Java revision'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

tasks.jar {
	enabled = false
}

tasks.bootJar {
	archiveBaseName.set("java-revision_app")
}

springBoot {
	mainClass = 'com.example.Application'
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
//			exclude 'revision/**'
		}
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'

	// Hibernate
	implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'

	// Database MySQL Connector
	runtimeOnly 'com.mysql:mysql-connector-j'

	// jsoup
	implementation 'org.jsoup:jsoup:1.21.2'

	// tests
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.mockito:mockito-core:5.21.0'
	testImplementation 'com.h2database:h2:2.3.232'

	testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
	testImplementation "org.testcontainers:junit-jupiter:1.20.4"
	testImplementation "org.testcontainers:mysql:1.20.4"
}

/*
Unit test + jacoco
 */
tasks.named('test') {
	/*
	useJUnitPlatform - informacje ze gradle ma wypisywac wyniki testÃ³w
	testLogging -
	 */
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.12"
	reportsDirectory = layout.buildDirectory.dir("reports/jacoco")
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtmlCustom')
	}
	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect {
					fileTree(dir: it, include: [
							'**/revision/unitTest/code/**'
					])
				})
		)
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.5
			}
		}
	}
	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect {
					fileTree(dir: it, include: [
							'**/revision/unitTest/code/**'
					])
				})
		)
	}
}
check.dependsOn jacocoTestCoverageVerification

/*  Tasks */
tasks.register('outFirstTask') {
	group = 'my-group'
	dependsOn 'outSecondTask', 'outThirdTask'

	doLast {
		println("homeDir: " + gradle.gradleHomeDir)
	}
}
tasks.register('outSecondTask') {
	group = 'my-group'
	description = 'java description'
	println('Second always printed')
}
tasks.register('outThirdTask') {
	group = 'my-group'
	doFirst {
		println('doFirst third')
	}
	doLast {
		println('doLast third')
	}
}

/*  defaultTasks -> uruchamia defaultowo taski przy komendzie gradle
*
* defaultTasks('outFirstTask')
*
* */